plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.3'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
}

group = 'aj.FiTracker'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(23)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	compileOnly 'jakarta.servlet:jakarta.servlet-api:6.1.0'
	implementation 'org.flywaydb:flyway-core:11.4.0'
	runtimeOnly 'org.flywaydb:flyway-database-postgresql:11.4.0'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa:3.4.4'
	implementation 'org.springframework.boot:spring-boot-starter-security:3.4.4'
	implementation 'org.springframework.boot:spring-boot-starter-web:3.4.4'
	compileOnly 'org.projectlombok:lombok:1.18.36'
	runtimeOnly 'com.h2database:h2:2.3.232'
	annotationProcessor 'org.projectlombok:lombok:1.18.36'
	testImplementation 'org.springframework.boot:spring-boot-starter-test:3.4.4'
	testImplementation 'org.springframework.security:spring-security-test:6.4.4'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.11.4'
	implementation 'org.springframework.boot:spring-boot-starter-validation:3.4.4'
	implementation 'org.postgresql:postgresql:42.7.5'
	testImplementation 'org.springframework.boot:spring-boot-starter-test:3.4.4'
	testImplementation 'org.testcontainers:junit-jupiter:1.20.6'
	testImplementation 'org.testcontainers:postgresql:1.20.6'
	implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'
	implementation 'org.springframework.security:spring-security-oauth2-authorization-server:1.4.2'
	implementation 'org.springframework.security:spring-security-oauth2-jose:6.4.4'
	implementation 'org.springframework.vault:spring-vault-core:3.1.2'
	implementation 'org.springframework.cloud:spring-cloud-vault-config:4.2.1'
	testImplementation "org.testcontainers:vault:1.20.6"
	implementation 'commons-codec:commons-codec:1.18.0'
	implementation 'org.bouncycastle:bcpkix-jdk18on:1.80'
	implementation 'ch.qos.logback:logback-classic:1.5.18'
	implementation 'com.github.loki4j:loki-logback-appender:1.5.2'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'


}


tasks.register('unitTest', Test) {
	useJUnitPlatform {
		excludeTags 'integration', 'integrationWithCSRF'
	}
	systemProperty 'spring.profiles.active', 'unit'
}

tasks.register('integrationTest', Test) {
	useJUnitPlatform {
		includeTags 'integration', 'integrationWithCSRF'
	}
	systemProperty 'spring.profiles.active', 'integration'
	shouldRunAfter 'unitTest'
}



tasks.register('buildWithoutTests') {
	group = 'build'
	description = 'Builds the project without running any tests.'

	dependsOn assemble
}

test {
	dependsOn unitTest
}

tasks.jacocoTestReport {
	dependsOn tasks.named('test'), tasks.named('integrationTest')
	executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

	reports {
		xml.required = true
		csv.required = false
		html.required = true
		html.outputLocation = layout.buildDirectory.dir('reports/jacoco')
	}
}

tasks.jacocoTestCoverageVerification {
	dependsOn tasks.jacocoTestReport
	violationRules {
		rule {
			limit {
				minimum = 0.5
			}
		}
	}
}

check {
	dependsOn tasks.jacocoTestCoverageVerification
}


sourceSets {
	test {
		resources {
			srcDirs = ['src/test/resources']
		}
	}
}